plugins {
    id 'com.android.application'
    id 'kotlin-android'
    id 'com.google.gms.google-services'
}

android {
    compileSdkVersion 31
    buildToolsVersion "30.0.3"

    defaultConfig {
        applicationId "com.cyberark.mfa"
        minSdkVersion 24
        targetSdkVersion 31
        versionCode 1
        versionName "1.0"

        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"

        manifestPlaceholders = [cyberarkIdentityHost: "@string/cyberark_auth_host",
                                cyberarkIdentityScheme: "@string/cyberark_auth_scheme"]
    }

    // [Option 1 - using "configs.xml"]
    // Update the res/values/configs.xml with the actual values
    // and remove "local.properties" keys-values and debug -> resValue keys-values


    // [Option 2(debug mode only) - using "local.properties"]
    // Add the following keys and the corresponding values to "local.properties"
    /* #CyberArk hosted login
    config_auth_system_url={auth_system_url}
    config_auth_host_url={auth_host_url}
    config_auth_client_id={auth_client_id}
    config_auth_app_id={auth_app_id}
    config_auth_response_type=code
    config_auth_scope=All
    config_auth_redirect_uri={auth_host}://{auth_scheme}/android/com.cyberark.mfa/callback
    config_auth_host={auth_host}
    config_auth_scheme={auth_scheme}

    #Google ReCaptcha V2 Settings
    config_recaptcha_key={recaptcha_key}

    #Step-up authentication using MFA widget
    config_native_login_url={native_login_url}
    config_widget_host_url={widget_host_url}
    config_widget_id={widget_id} */
    // and update debug -> resValue keys-values


    def localProperties = new Properties()
    localProperties.load(new FileInputStream(rootProject.file("local.properties")))

    buildTypes {
        debug {
            // CyberArk hosted login
            resValue("string", "config_auth_system_url", localProperties['config_auth_system_url'])
            resValue("string", "config_auth_host_url", localProperties['config_auth_host_url'])
            resValue("string", "config_auth_client_id", localProperties['config_auth_client_id'])
            resValue("string", "config_auth_app_id", localProperties['config_auth_app_id'])
            resValue("string", "config_auth_response_type", localProperties['config_auth_response_type'])
            resValue("string", "config_auth_scope", localProperties['config_auth_scope'])
            resValue("string", "config_auth_redirect_uri", localProperties['config_auth_redirect_uri'])
            resValue("string", "config_auth_host", localProperties['config_auth_host'])
            resValue("string", "config_auth_scheme", localProperties['config_auth_scheme'])

            // Google ReCaptcha V2 Settings
            resValue("string", "config_recaptcha_key", localProperties['config_recaptcha_key'])

            // Step-up authentication using MFA widget
            resValue("string", "config_native_login_url", localProperties['config_native_login_url'])
            resValue("string", "config_widget_host_url", localProperties['config_widget_host_url'])
            resValue("string", "config_widget_id", localProperties['config_widget_id'])
        }
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }
    }
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }
    kotlinOptions {
        jvmTarget = '1.8'
    }
}

dependencies {
    implementation 'androidx.core:core-ktx:1.7.0'
    implementation 'androidx.appcompat:appcompat:1.4.1'
    implementation 'com.google.android.material:material:1.5.0'
    implementation 'androidx.constraintlayout:constraintlayout:2.1.3'

    //In-build test framework libs
    testImplementation 'junit:junit:4.13.2'
    androidTestImplementation 'androidx.test.ext:junit:1.1.3'
    androidTestImplementation 'androidx.test.espresso:espresso-core:3.4.0'

    //FCM libs
    implementation platform('com.google.firebase:firebase-bom:28.4.0')
    implementation 'com.google.firebase:firebase-messaging-ktx'
    implementation 'com.google.firebase:firebase-analytics-ktx'

    //LifeCycle libs
    implementation 'androidx.lifecycle:lifecycle-runtime-ktx:2.4.1'
    implementation "androidx.lifecycle:lifecycle-extensions:2.2.0"
    implementation 'com.google.code.gson:gson:2.8.9'

    //Volley network operations lib
    implementation 'com.android.volley:volley:1.2.1'

    // CyberArk Identity Android SDK lib
    implementation 'com.cyberark.identity:android-sdk:1.0.1'
}